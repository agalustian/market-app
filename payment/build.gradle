import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "java"
    id "org.springframework.boot" version "3.5.6"
    id "io.spring.dependency-management" version "1.1.7"
    id "org.openapi.generator" version "7.12.0"
}

group = "ru.market.payment"
version = "0.0.1"
description = "Market payment application"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', "$projectDir/build/generated/src/main/java"]
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-webflux"
//    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "jakarta.validation:jakarta.validation-api:3.1.1"
    implementation "io.swagger.core.v3:swagger-annotations:2.2.28"
    implementation "org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.13"

    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "io.projectreactor:reactor-test"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

tasks.named("test") {
    useJUnitPlatform {
        filter {
            exclude("**/integration/**")
        }
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register("integrationTest", Test) {
    useJUnitPlatform {
        filter {
            include("**/integration/**")
        }
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register("generatePaymentServer", GenerateTask) {
    generatorName.set("spring")
    inputSpec.set("$projectDir/src/main/resources/payment.yaml")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.market.payment.server.domain")
    invokerPackage.set("ru.market.payment.server")
    apiPackage.set("ru.market.payment.server.api")
    configOptions.set([
            hideGenerationTimestamp: "true",
            requestMappingMode     : "controller",
            interfaceOnly          : "true",
            library                : "spring-boot",
            reactive               : "true",
            useSpringBoot3         : "true",
            useJakartaEe           : "true",
            useTags                : "true",
            dateLibrary            : "java8",
            openApiNullable        : "false",
            serializableModel      : "true",
            returnSuccessCode      : "true",
            delegatePattern        : "true"
    ])
}

compileJava.dependsOn tasks.generatePaymentServer